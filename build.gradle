import ru.otus.Versions

plugins {
    id 'java'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'com.github.johnrengelman.shadow' version "7.1.2" apply false
    id "org.springframework.boot" version "2.7.2" apply false
    id "com.google.cloud.tools.jib" version "3.2.1" apply false
    id "fr.brouillard.oss.gradle.jgitver" version "0.10.0-rc03" apply false
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

allprojects {

    group 'ru.otus'
    repositories {
        mavenLocal()
        mavenCentral()
    }
    apply plugin: "io.spring.dependency-management"//Плагин, который дает в gradle подтягивать зависимости
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.0"
            mavenBom "org.testcontainers:testcontainers-bom:1.17.1"
            mavenBom "com.google.protobuf:protobuf-bom:3.21.1"
        }
        dependencies {
            dependency "com.google.guava:guava:${Versions.guavaLastVersion}"
            dependency "org.openjdk.jmh:jmh-core:${Versions.jmh}"
            dependency "org.openjdk.jmh:jmh-generator-annprocess:${Versions.jmh}"
            dependency "org.ow2.asm:asm-commons:${Versions.asm}"
            dependency "org.jetbrains:annotations:${Versions.jet}"
        }
    }
    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
        resolutionStrategy {
            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("net.java.dev.jna:jna:5.8.0")
            force("com.google.errorprone:error_prone_annotations:2.7.1")
        }
    }

}